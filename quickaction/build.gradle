plugins {
  id "com.github.dcendents.android-maven" version "1.5"
  id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'com.android.library'

group = rootProject.group
version = rootProject.version
description = rootProject.description

android {
  compileSdkVersion ANDROID_TARGET_SDK
  buildToolsVersion ANDROID_BUILD_TOOLS

  defaultConfig {
    minSdkVersion ANDROID_MIN_SDK
    targetSdkVersion ANDROID_TARGET_SDK
  }
  compileOptions {
    sourceCompatibility JAVA_VERSION
    targetCompatibility JAVA_VERSION
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError true
    checkAllWarnings true
    warningsAsErrors true
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:support-annotations:25.0.0'
}

task echo() {
  println "$project.group:$project.name:$project.version"
  println Calendar.instance.get(Calendar.YEAR)
  println project.description
  println project.bintrayUser
  println 'https://github.com/' + project.githubRepo
  println 'https://github.com/' + project.githubRepo + '/issues'
  println 'https://github.com/' + project.githubRepo + '.git'
  if (project.hasProperty('bintrayOrg')) println project.getProperty('bintrayOrg')
}

install {
  repositories.mavenInstaller {
    pom.project {
      name 'QuickAction'
      inceptionYear 2016
      description = project.description
      url = 'https://github.com/' + project.githubRepo
      packaging 'aar'
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
      }
      developers {
        developer {
          id 'piruin'
          name 'Blast Piruin Panichphol'
          email 'piruin.p@gmail'
          url 'https://piruin.me'
        }
        developer {
          id 'lorensiuswlt'
          name 'Lorensius W. L. T'
          email 'lorenz@londatiga.net'
          url 'http://www.londatiga.net'
        }
      }
      contributors {
        contributor {
          name 'Kevin Peck'
          email 'kevinwpeck@gmail.com'
        }
      }
      scm {
        connection 'https://github.com/' + project.githubRepo + '.git'
        developerConnection 'https://github.com/' + project.githubRepo + '.git'
        url 'https://github.com/' + project.githubRepo
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') :
    System.getenv('BINTRAY_USER') ?: ''
  key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') :
    System.getenv('BINTRAY_KEY') ?: ''
  configurations = ['archives']
  publish = true
  override = true
  pkg {
    repo = 'maven'
    name = 'QuickAction'
    desc = project.description
    websiteUrl = 'https://github.com/' + project.githubRepo
    issueTrackerUrl = 'https://github.com/' + project.githubRepo + '/issues'
    vcsUrl = 'https://github.com/' + project.githubRepo + '.git'
    githubRepo = project.githubRepo
    githubReleaseNotesFile = 'README.md'
    licenses = ['Apache-2.0']
    publicDownloadNumbers = true
    version {
      name = project.version
      released = new Date()
      vcsTag = project.version
    }
  }
}